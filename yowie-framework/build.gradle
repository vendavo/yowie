apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

jar {
    baseName = 'yowie-framework'
}

configurations.create('angular')

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir new File(buildDir, 'yowie-web')
        }
    }
}

dependencies {

    compile project(':yowie-web')
    compile project(':yowie-api')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-websocket"
    compile "org.springframework.boot:spring-boot-starter-integration"
    compile "org.springframework.integration:spring-integration-websocket"
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile("org.springframework.boot:spring-boot-starter-groovy-templates") {
        exclude group: 'org.codehaus.groovy'
    }
    compile 'org.springframework.integration:spring-integration-event:4.3.7.RELEASE'

    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.codehaus.groovy:groovy-all:$groovy.version"

    compile "org.apache.mesos:mesos:$mesos.version"

    compile "com.fasterxml.jackson.core:jackson-core:$jackson.version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson.version"

    compile 'com.netflix.feign:feign-core:8.18.0'
    compile 'com.netflix.feign:feign-jackson:8.18.0'

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.skyscreamer:jsonassert:1.4.0'
    testCompile 'com.jayway.jsonpath:json-path-assert:2.2.0'
    testCompile 'com.github.tomakehurst:wiremock:1.56'
    testCompile 'cglib:cglib-nodep:3.2.4'

    angular project(':yowie-web')
}

task extractDeps(type: Copy) {

    from {
        configurations.angular.collect { zipTree(it) }
    }
    into new File(buildDir, "yowie-web/static")
}

task bintrayInfo(type: Copy) {
    from 'bintray-info.json'
    into 'build'
    rename {
        "${project.name}-${version}-bintray-info.json"
    }
    expand(
        version: version
    )
}

artifacts {
  archives file: file("${buildDir}/${project.name}-${version}-bintray-info.json"), builtBy: bintrayInfo
}

project.tasks.processResources.dependsOn(project.tasks.extractDeps)
